#include "./lib/ActiveWindowMirror.hsp"
#include "./lib/PopupMenu.hsp"
#include "./lib/WindowFollower.hsp"
#include "./lib/WinUtil.hsp"
#include "./lib/TimerLoop.hsp"

#uselib "user32.dll"
#func GetForegroundWindow "GetForegroundWindow"
#func GetDesktopWindow   "GetDesktopWindow"
#func GetShellWindow     "GetShellWindow"

#uselib "kernel32.dll"
#cfunc GetTickCount "GetTickCount"

;==================================================
; ビルドオプション
;==================================================

#cmpopt ppout 0
#cmpopt optcode 1
#cmpopt optinfo 0
#cmpopt varname 0
#cmpopt optprm 1

#packopt name        "ShareMirror"
#packopt runtime     "hsprt"
#packopt upx         "0"
#packopt type        0
#packopt xsize       1
#packopt ysize       1
#packopt hide        1
#packopt orgpath     1
#packopt version     "version.txt"
#packopt lang        "1041"

;==================================================
; ID / 設定 定数
;==================================================
#define IDM_EXIT        1

#define TITLE_NAME      "ShareMirror"
#define WIN_MAIN        2
#define BUF_CAPTURE     3

#define INIT_W          320
#define INIT_H          240

; タイマー設定（TimerLoop用）
#define TIMER_ID        1
#define TIMER_MS        100

;==================================================
; ミラー制御設定
;==================================================
IGNORE_DESKTOP = 1        ; 1=デスクトップ無視, 0=無効
SWITCH_DELAY_MS = 3000    ; 切替待ち時間(ms) / 0で即切替

;--------------------------------------------------
; ウィンドウ作成（bgscr）
;--------------------------------------------------
; メインウィンドウは非表示とする
gsel 0, -1

; 事前に最大まで確保（縮小は可能）、p4 = 2 で非表示で作成
bgscr WIN_MAIN, ginfo_dispx, ginfo_dispy, 2
width INIT_W, INIT_H

; 画面を非表示の状態で青背景でタイトルを表示
title TITLE_NAME
color 0, 0, 255
boxf
color 255, 255, 255
font "Meiryo UI"
mes TITLE_NAME
mes "右クリックメニューから終了できます"
pos 0, 0 ; カレントポジションを左上に戻す

gsel WIN_MAIN, 1 ; 表示

; キャプチャ先バッファ
buffer BUF_CAPTURE

; bufferで操作先が変わるので、必ずメインへ戻す
gsel WIN_MAIN

;--------------------------------------------------
; モジュール生成
;--------------------------------------------------
newmod cap, ActiveWindowMirror, BUF_CAPTURE, 0
newmod menu, PopupMenu
addItem menu, IDM_EXIT, "終了"
newmod follower, WindowFollower, hwnd
newmod tloop, TimerLoop, TIMER_ID, TIMER_MS, *tick_capture

;--------------------------------------------------
; イベント登録（WIN_MAINに対して）
;--------------------------------------------------
gsel WIN_MAIN
onclick gosub *ev_click
oncmd  gosub *ev_timer, tl_messageId(tloop)
onexit goto  *app_exit

; タイマー開始
tl_start tloop, hwnd

stop


;--------------------------------------------------
; WM_TIMER イベント入口
;--------------------------------------------------
*ev_timer
    tl_dispatch tloop
    return


;--------------------------------------------------
; 周期処理本体（キャプチャ＆描画）
;--------------------------------------------------
*tick_capture
    ; 現在の前面ウィンドウ
    fg = GetForegroundWindow()

    ; 自分自身は常に無視
    if fg = hwnd : return

    ;------------------------------
    ; デスクトップ無視
    ;------------------------------
    if IGNORE_DESKTOP {
        if fg = GetDesktopWindow() : return
        if fg = GetShellWindow()   : return
    }

    ;------------------------------
    ; 初回は即採用
    ;------------------------------
    if lastTargetHwnd = 0 {
        lastTargetHwnd = fg
        pendingHwnd = 0
        pendingStart = 0
	    gosub *do_capture
	    return
    }

    ;------------------------------
    ; 同じウィンドウなら普通に更新
    ;------------------------------
    if fg = lastTargetHwnd {
        pendingHwnd = 0
        pendingStart = 0
	    gosub *do_capture
	    return
    }

    ;------------------------------
    ; 切替ディレイなし
    ;------------------------------
    if SWITCH_DELAY_MS <= 0 {
        lastTargetHwnd = fg
        pendingHwnd = 0
        pendingStart = 0
	    gosub *do_capture
	    return
    }

    ;------------------------------
    ; 切替候補の管理
    ;------------------------------
    if fg != pendingHwnd {
        pendingHwnd = fg
        pendingStart = GetTickCount()
        return
    }

    ; 待ち時間チェック
    now = GetTickCount()
    if (now - pendingStart) < SWITCH_DELAY_MS : return

    ; 規定時間経過 → 切替確定
    lastTargetHwnd = pendingHwnd
    pendingHwnd = 0
    pendingStart = 0

    gosub *do_capture
    return

*do_capture
    w = 0 : h = 0
    ok = capture(cap, hwnd, w, h)
    if ok = 0 : return

    ; サイズ追従
    EnsureSize follower, w, h

    ; 描画
    gsel WIN_MAIN
    redraw 0
    gcopy BUF_CAPTURE, 0, 0, w, h
    redraw 1
    return


;--------------------------------------------------
; マウスクリックイベント
;--------------------------------------------------
*ev_click
    ; 左クリック：どこでもドラッグ
    if wparam = 1 {
        DragMove hwnd
        return
    }

    ; 右クリック：終了メニュー
    if wparam = 2 {
        id = show(menu, ginfo_mx, ginfo_my, hwnd)
        if id = IDM_EXIT : goto *app_exit
    }
    return


;--------------------------------------------------
; 終了処理
;--------------------------------------------------
*app_exit
    ; タイマー停止
    tl_stop tloop, hwnd

    ; モジュール解放
    delmod menu
    delmod cap
    delmod follower
    delmod tloop

    end
